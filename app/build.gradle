apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.andyradionov.topnews"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', "API_KEY", news_api_key
        buildConfigField 'String', "API_URL", news_api_url
        buildConfigField 'String', "SORT_BY", news_api_sort_by
        buildConfigField 'int', "PAGE_SIZE", news_api_page_size
        buildConfigField 'int', "SEARCH_SIZE", news_api_search_size
        buildConfigField 'String', "ICONS_URL", icons_api_url
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext.versions = [ANDROID_SUPPORT  : "28.0.0",
                CONSTRAINT_LAYOUT: "1.1.3",
                PICASSO          : "2.71828",
                ROUND_IMAGE      : "2.3.0",
                PRETTY_TIME      : "4.0.1.Final",
                RX_JAVA          : "2.1.16",
                RX_ANDROID       : "2.1.0",
                RX_RELAY         : "2.1.0",
                GSON             : "2.8.5",
                RETROFIT         : "2.4.0",
                DAGGER           : "2.16",
                MOXY             : "1.5.5",
                CICERONE         : "4.0.2",
                ADAPTER_DELEGATES: "3.1.0",
                ROOM             : "1.1.1",
                JUNIT            : "4.12",
                TEST_RUNNER      : "1.0.2",
                ESPRESSO         : "3.0.2"]

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$versions.ANDROID_SUPPORT"
    implementation "com.android.support:exifinterface:$versions.ANDROID_SUPPORT"
    implementation "com.android.support:animated-vector-drawable:$versions.ANDROID_SUPPORT"
    implementation "com.android.support:recyclerview-v7:$versions.ANDROID_SUPPORT"
    implementation "com.android.support:cardview-v7:$versions.ANDROID_SUPPORT"
    implementation "com.android.support:support-v4:$versions.ANDROID_SUPPORT"
    implementation "com.android.support:design:$versions.ANDROID_SUPPORT"
    implementation "com.android.support.constraint:constraint-layout:$versions.CONSTRAINT_LAYOUT"

    //Images
    implementation "com.squareup.picasso:picasso:$versions.PICASSO"
    implementation "com.makeramen:roundedimageview:$versions.ROUND_IMAGE"

    //PrettyTime
    implementation "org.ocpsoft.prettytime:prettytime:$versions.PRETTY_TIME"

    //Rx
    implementation "io.reactivex.rxjava2:rxandroid:$versions.RX_ANDROID"
    implementation "io.reactivex.rxjava2:rxjava:$versions.RX_JAVA"
    implementation "com.jakewharton.rxrelay2:rxrelay:$versions.RX_RELAY"

    //Retrofit
    implementation "com.google.code.gson:gson:$versions.GSON"
    implementation "com.squareup.retrofit2:retrofit:$versions.RETROFIT"
    implementation "com.squareup.retrofit2:converter-gson:$versions.RETROFIT"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.RETROFIT"

    //DI Dagger
    implementation "com.google.dagger:dagger:$versions.DAGGER"
    implementation "com.google.dagger:dagger-android-support:$versions.DAGGER"
    kapt "com.google.dagger:dagger-android-processor:$versions.DAGGER"
    kapt "com.google.dagger:dagger-compiler:$versions.DAGGER"

    //MVP Moxy
    implementation "com.arello-mobile:moxy:$versions.MOXY"
    implementation "com.arello-mobile:moxy-android:$versions.MOXY"
    implementation "com.arello-mobile:moxy-app-compat:$versions.MOXY"
    kapt "com.arello-mobile:moxy-compiler:$versions.MOXY"

    //Navigation Cicerone
    implementation "ru.terrakok.cicerone:cicerone:$versions.CICERONE"

    //Adapters Delegates
    implementation "com.hannesdorfmann:adapterdelegates3:$versions.ADAPTER_DELEGATES"

    //DB Room
    implementation "android.arch.persistence.room:runtime:$versions.ROOM"
    implementation "android.arch.persistence.room:rxjava2:$versions.ROOM"
    kapt "android.arch.persistence.room:compiler:$versions.ROOM"

    //Testing
    testImplementation "junit:junit:$versions.JUNIT"
    androidTestImplementation "com.android.support.test:runner:$versions.TEST_RUNNER"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.ESPRESSO"
}
